<program> ===> <otherFunctions> <mainFunction>
<mainFunction> ===> TK_MAIN <stmts> TK_END
<otherFunctions> ===> <function> <otherFunctions> | TK_EPS
<function> ===> TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
<input_par> ===> TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par> ===> TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR | TK_EPS
<parameter_list> ===> <dataType> TK_ID <remaining_list>
<dataType> ===> <primitiveDatatype> | <constructedDatatype>
<primitiveDatatype> ===> TK_INT | TK_REAL
<constructedDatatype> ===> TK_RECORD TK_RUID | TK_UNION TK_RUID | TK_RUID
<remaining_list> ===> TK_COMMA <parameter_list> | TK_EPS
<stmts> ===> <typeDefinitions> <declarations> <otherStmts> <returnStmt>
<typeDefinitions> ===> <typeDefinition> <typeDefinitions> | <definetypestmt> <typeDefinitions> | TK_EPS
<typeDefinition> ===> TK_RECORD TK_RUID <fieldDefinitions> TK_ENDRECORD
<typeDefinition> ===> TK_UNION TK_RUID <fieldDefinitions> TK_ENDUNION
<fieldDefinitions> ===> <fieldDefinition> <fieldDefinition> <moreFields>
<fieldDefinition> ===> TK_TYPE <dataType> TK_COLON TK_FIELDID TK_SEM
<moreFields> ===> <fieldDefinition> <moreFields> | TK_EPS
<declarations> ===> <declaration> <declarations> | TK_EPS
<declaration> ===> TK_TYPE <dataType> TK_COLON TK_ID <global_or_not> TK_SEM
<global_or_not> ===> TK_COLON TK_GLOBAL | TK_EPS
<otherStmts> ===> <stmt> <otherStmts> | TK_EPS
<stmt> ===> <assignmentStmt> | <iterativeStmt> | <conditionalStmt> | <ioStmt> | <funCallStmt>
<assignmentStmt> ===> <singleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM
<singleOrRecId> ===> TK_ID <recId>
<recId> ===> TK_DOT TK_FIELDID <recId> | TK_EPS
<funCallStmt> ===> <outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
<outputParameters> ===> TK_SQL <idList> TK_SQR TK_ASSIGNOP | TK_EPS
<inputParameters> ===> TK_SQL <idList> TK_SQR
<iterativeStmt> ===> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt> <otherStmts> TK_ENDWHILE
<conditionalStmt> ===> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt> <otherStmts> <conditionalStmtContd>
<conditionalStmtContd> ===> TK_ELSE <stmt> <otherStmts> TK_ENDIF | TK_ENDIF
<ioStmt> ===> TK_READ TK_OP <var> TK_CL TK_SEM | TK_WRITE TK_OP <var> TK_CL TK_SEM
<arithmeticExpression> ===> <arithmeticExpression2> <arithmeticExpressionContd>
<arithmeticExpressionContd> ===> <operator1> <arithmeticExpression2> <arithmeticExpressionContd> | TK_EPS
<operator1> ===> TK_PLUS | TK_MINUS
<arithmeticExpression2> ===> <arithmeticExpression3> <arithmeticExpression2Contd>
<arithmeticExpression2Contd> ===> <operator2> <arithmeticExpression3> <arithmeticExpression2Contd> | TK_EPS
<operator2> ===> TK_MUL | TK_DIV
<arithmeticExpression3> ===> TK_OP <arithmeticExpression> TK_CL | <var>
<booleanExpression> ===> TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL
<booleanExpression> ===> <var> <relationalOp> <var>
<booleanExpression> ===> TK_NOT TK_OP <booleanExpression> TK_CL
<var> ===> <singleOrRecId> | TK_NUM | TK_RNUM
<logicalOp> ===> TK_AND | TK_OR
<relationalOp> ===> TK_LT | TK_LE | TK_EQ | TK_GT | TK_GE | TK_NE
<returnStmt> ===> TK_RETURN <optionalReturn> TK_SEM
<optionalReturn> ===> TK_SQL <idList> TK_SQR | TK_EPS
<idList> ===> TK_ID <more_ids>
<more_ids> ===> TK_COMMA <idList> | TK_EPS
<definetypestmt> ===> TK_DEFINETYPE <A> TK_RUID TK_AS TK_RUID
<A> ===> TK_RECORD | TK_UNION